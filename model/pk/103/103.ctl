$PROBLEM From bbr: see 103.yaml for details

$INPUT C NUM ID TIME SEQ CMT EVID AMT DV AGE WT HT EGFR ALB BMI SEX AAG
       SCR AST ALT CP TAFD TAD LDOS MDV BLQ PHASE

$DATA ../../../data/derived/pk.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN4 TRANS4

$PK
 
;log transformed PK parms
 
V2WT = LOG(WT/70)
CLWT = LOG(WT/70)*0.75
V3WT = LOG(WT/70)
QWT  = LOG(WT/70)*0.75

KA   = EXP(THETA(1)+ETA(1))
V2   = EXP(THETA(2)+V2WT+ETA(2))
CL   = EXP(THETA(3)+CLWT+ETA(3))
V3   = EXP(THETA(4)+V3WT)
Q    = EXP(THETA(5)+QWT) 

S2 = V2/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED+EPS(1)

$THETA  ; log values
(0.42)   ;  1 KA (1/hr) - 1.5
(4.06)   ;  2 V2 (L) - 60
(1.11)     ;  3 CL (L/hr) - 3.5
(4.28)     ;  4 V3 (L) - 70
(1.31)     ;  5 Q  (L/hr) - 4


$OMEGA BLOCK(3)
0.02   ;ETA(KA)
0.01 0.02   ;ETA(V2)
0.01 0.01 0.09   ;ETA(CL)

$SIGMA
1000     ; 1 add error

$EST MAXEVAL=9999 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1 RANMETHOD=P MSFO=./103.MSF 
$COV PRINT=E RANMETHOD=P
$TABLE NUM IPRED NPDE CWRES NOPRINT ONEHEADER RANMETHOD=P FILE=103.tab
$TABLE NUM CL V2 Q V3 KA ETAS(1:LAST) NOAPPEND NOPRINT ONEHEADER FILE=103par.tab
